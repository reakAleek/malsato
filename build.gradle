plugins {
    id "org.jetbrains.kotlin.jvm"               version "1.2.70"        apply false
    id "org.jetbrains.kotlin.plugin.spring"     version "1.2.70"        apply false
    id "org.jetbrains.kotlin.plugin.jpa"        version "1.2.70"        apply false
    id "org.jetbrains.kotlin.kapt"              version "1.2.70"        apply false
    id "org.jetbrains.kotlin.plugin.noarg"      version "1.2.70"        apply false
    id "org.springframework.boot"               version "2.0.5.RELEASE" apply false
    id "io.spring.dependency-management"        version "1.0.6.RELEASE" apply false
    id "org.flywaydb.flyway"                    version "5.1.4"         apply false
}

ext {
    mapstructVersion = "1.2.0.Final"
    springBootVersion = "2.0.5.RELEASE"
}


allprojects {
    group "com.malsato"
    version "1.0-SNAPSHOT"
}

subprojects {
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "org.jetbrains.kotlin.plugin.spring"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.jetbrains.kotlin.plugin.noarg"

    noArg.annotation("com.malsato.common.util.NoArgsConstructor")
    bootJar.enabled = false
    jar.enabled = true

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile "org.jetbrains.kotlin:kotlin-reflect"
        compile "org.modelmapper:modelmapper:2.3.0"
        testCompile "org.springframework.boot:spring-boot-starter-test"
        testCompile "com.h2database:h2:1.4.196"
        testCompile "com.willowtreeapps.assertk:assertk-jvm:0.12"
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

}

project(":common") {
}

project(":rest") {
    dependencies {
        compile project(":business")
        compile "org.springframework.boot:spring-boot-starter-web"
    }
}

project(":web") {
    dependencies {
        compile project(":business")
        compile "org.springframework.boot:spring-boot-starter-web"
    }
}

project(":data") {

    apply plugin: "org.jetbrains.kotlin.plugin.jpa"

    dependencies {
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "mysql:mysql-connector-java"
        compile "javax.xml.bind:jaxb-api"
        compile "com.fasterxml.jackson.module:jackson-module-kotlin"
    }
}

project(":business") {
    dependencies {
        compile project(":common")
        compile project(":data")
    }
}